yosys -import

source $::env(SCRIPTS_DIR)/util.tcl
erase_non_stage_variables synth

# If using a cached, gate level netlist, then copy over to the results dir with
# preserve timestamps flag set. If you don't, subsequent runs will cause the
# floorplan step to be re-executed.
if { [env_var_exists_and_non_empty SYNTH_NETLIST_FILES] } {
  if { [llength $::env(SYNTH_NETLIST_FILES)] == 1 } {
    log_cmd exec cp -p $::env(SYNTH_NETLIST_FILES) $::env(RESULTS_DIR)/1_2_yosys.v
  } else {
    # The date should be the most recent date of the files, but to
    # keep things simple we just use the creation date
    log_cmd exec cat {*}$::env(SYNTH_NETLIST_FILES) > $::env(RESULTS_DIR)/1_2_yosys.v
  }
  log_cmd exec cp -p $::env(SDC_FILE) $::env(RESULTS_DIR)/1_synth.sdc
  if { [env_var_exists_and_non_empty CACHED_REPORTS] } {
    log_cmd exec cp -p {*}$::env(CACHED_REPORTS) $::env(REPORTS_DIR)/.
  }
  exit
}

proc read_checkpoint { file } {
  # We are reading a Yosys checkpoint
  if { [file extension $file] == ".json" } {
    read_json $file
  } else {
    read_rtlil $file
  }
}

proc read_design_sources { } {
  # We are reading Verilog sources
  source $::env(SCRIPTS_DIR)/synth_stdcells.tcl

  # Setup verilog include directories
  set vIdirsArgs ""
  if { [env_var_exists_and_non_empty VERILOG_INCLUDE_DIRS] } {
    foreach dir $::env(VERILOG_INCLUDE_DIRS) {
      lappend vIdirsArgs "-I$dir"
    }
    set vIdirsArgs [join $vIdirsArgs]
  }

  if { [env_var_equals SYNTH_HDL_FRONTEND slang] } {
    # slang requires all files at once
    plugin -i slang
    yosys read_slang -D SYNTHESIS --keep-hierarchy --compat=vcs \
      --ignore-assertions --no-implicit-memories --top $::env(DESIGN_NAME) \
      {*}$vIdirsArgs {*}$::env(VERILOG_FILES) {*}[env_var_or_empty VERILOG_DEFINES]
    # Workaround for yosys-slang#119
    setattr -unset init
  } elseif { [env_var_equals SYNTH_HDL_FRONTEND verific] } {
    if { [env_var_exists_and_non_empty VERILOG_INCLUDE_DIRS] } {
      verific -vlog-incdir {*}$::env(VERILOG_INCLUDE_DIRS)
    }
    if { [env_var_exists_and_non_empty VERILOG_DEFINES] } {
      verific -vlog-define {*}$::env(VERILOG_DEFINES)
    }
    verific -sv2012 {*}$::env(VERILOG_FILES)
    verific -import -no-split-complex-ports $::env(DESIGN_NAME)
  } elseif { ![env_var_exists_and_non_empty SYNTH_HDL_FRONTEND] } {
    verilog_defaults -push
    if { [env_var_exists_and_non_empty VERILOG_DEFINES] } {
      verilog_defaults -add {*}$::env(VERILOG_DEFINES)
    }
    foreach file $::env(VERILOG_FILES) {
      read_verilog -defer -sv {*}$vIdirsArgs $file
    }
    verilog_defaults -pop
  } else {
    error "Unrecognized HDL frontend: $::env(SYNTH_HDL_FRONTEND)"
  }

  # Read platform specific mapfile for OPENROAD_CLKGATE cells
  if { [env_var_exists_and_non_empty CLKGATE_MAP_FILE] } {
    read_verilog -defer $::env(CLKGATE_MAP_FILE)
  }

  if { [env_var_exists_and_non_empty SYNTH_BLACKBOXES] } {
    hierarchy -check -top $::env(DESIGN_NAME)
    foreach m $::env(SYNTH_BLACKBOXES) {
      blackbox $m
    }
  }
}

if { $::env(ABC_AREA) } {
  puts "Using ABC area script."
  set abc_script $::env(SCRIPTS_DIR)/abc_area.script
} else {
  puts "Using ABC speed script."
  set abc_script $::env(SCRIPTS_DIR)/abc_speed.script
}

# set abc_retime_script_for_two_phase $::env(SCRIPTS_DIR)/abc_retime_for_two_phase.script
set abc_retime_script_for_two_phase $::env(SCRIPTS_DIR)/$::env(ABC_RETIME_TWO_PHASE)

# Create argument list for stat
set lib_args ""
foreach lib $::env(LIB_FILES) {
  append lib_args "-liberty $lib "
}

# Exclude dont_use cells. This includes macros that are specified via
# LIB_FILES and ADDITIONAL_LIBS that are included in LIB_FILES.
set lib_dont_use_args ""
if { [env_var_exists_and_non_empty DONT_USE_CELLS] } {
  foreach cell $::env(DONT_USE_CELLS) {
    lappend lib_dont_use_args -dont_use $cell
  }
}

# Technology mapping for cells
set abc_args [list -script $abc_script \
  {*}$lib_args {*}$lib_dont_use_args -constr $::env(OBJECTS_DIR)/abc.constr]

set abc_args_for_retiming [list -script $abc_retime_script_for_two_phase \
  {*}$lib_args {*}$lib_dont_use_args -D 1 -constr $::env(OBJECTS_DIR)/abc.constr -keepff -dff]

if { [env_var_exists_and_non_empty SDC_FILE_CLOCK_PERIOD] } {
  puts "Extracting clock period from SDC file: $::env(SDC_FILE_CLOCK_PERIOD)"
  set fp [open $::env(SDC_FILE_CLOCK_PERIOD) r]
  set clock_period [string trim [read $fp]]
  if { $clock_period != "" } {
    puts "Setting clock period to $clock_period"
    lappend abc_args -D $clock_period
  }
  close $fp
}

set constr [open $::env(OBJECTS_DIR)/abc.constr w]
puts $constr "set_driving_cell $::env(ABC_DRIVER_CELL)"
puts $constr "set_load $::env(ABC_LOAD_IN_FF)"
close $constr

proc convert_liberty_areas { } {
  cellmatch -derive_luts =A:liberty_cell
  # find a reference nand2 gate
  set found_cell ""
  set found_cell_area ""
  # iterate over all cells with a nand2 signature
  foreach cell [tee -q -s result.string select -list-mod =*/a:lut=4'b0111 %m] {
    if { ![rtlil::has_attr -mod $cell area] } {
      puts "Cell $cell missing area information"
      continue
    }
    set area [rtlil::get_attr -string -mod $cell area]
    if { $found_cell == "" || $area < $found_cell_area } {
      set found_cell $cell
      set found_cell_area $area
    }
  }
  if { $found_cell == "" } {
    error "reference nand2 cell not found"
  }

  # convert the area on all Liberty cells to a gate number equivalent
  foreach box [tee -q -s result.string select -list-mod =A:area =A:liberty_cell %i] {
    set area [rtlil::get_attr -mod -string $box area]
    set gate_eq [expr int($area / $found_cell_area)]
    rtlil::set_attr -mod -uint $box gate_cost_equivalent $gate_eq
  }
}

proc connect_clk {cell_name clock_pin_name target_clk_port_name} {
    # Refer to this GitHub PR for more information on how to get the output of a Yosys command into a
    # Tcl variable: https://github.com/YosysHQ/yosys/pull/3349
    tee -q -s cells_to_update_scratchpad select -list c:$cell_name
    set cells_to_update [scratchpad -copy cells_to_update_scratchpad result.string]
    set cells_to_update [split $cells_to_update \n]

    # Remove empty element
    set index [lsearch $cells_to_update ""]
    set cells_to_update [lreplace $cells_to_update $index $index]
    set DFF_list [list]

    # Remove the name of the top module from the names of the cells
    foreach cell $cells_to_update {
        # How to create a substring: Inspired by this StackOverflow answer:
        # https://stackoverflow.com/a/15924092
        lappend DFF_list [string range $cell [expr {[string first "/" $cell]} + 1] end]
    }

    # Connect clk_2 to the clock port of each cell
    foreach cell $DFF_list {
        connect -port $cell $clock_pin_name $target_clk_port_name
    }
}

proc check_logical_equivalence {top_module gold gate abc_args lib_args lib_dont_use_args} {
    puts "Perform equivalence checking"
    puts "Save a backup that won't get deleted by this function"
    design -save backup_1

    design -load $gold
    abc {*}$abc_args
    dfflibmap {*}$lib_args {*}$lib_dont_use_args
    design -stash new_gold

    design -load $gate
    dfflibmap {*}$lib_args {*}$lib_dont_use_args
    design -stash new_gate

    puts "Create new modules"
    design -copy-from new_gold -as gold $top_module
    design -copy-from new_gate -as gate $top_module

    equiv_make -inames gold gate equiv
    yosys cd equiv

    foreach lib $::env(LIB_FILES) {
      yosys read_liberty -wb -ignore_miss_func $lib
    }
    prep -flatten -top equiv
    opt_clean -purge

    equiv_simple -undef -short -seq 1
    equiv_induct -undef -seq 4
    equiv_status -assert

    puts "Restore the design"
    design -load backup_1
}

proc legal_modules { map_file } {
    puts "reading $map_file"
    set infile [open $map_file r]

    set module_list [list]

    while {[gets $infile line] != -1} {
        if {[regexp {^\s*module\s+(\\?\$\S+)} $line -> name]} {
            set name [string trimleft $name "\\"]

            # skip latches
            if {[string match {$_DLATCH*} $name]} {
                continue
            }

            lappend module_list -cell $name 01
        }
    }

    close $infile
    return $module_list
}